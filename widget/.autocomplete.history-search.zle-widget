#!/bin/zsh
setopt localoptions extendedglob nolistbeep

local lbuffer=$LBUFFER rbuffer=$RBUFFER delim=$'[|&!;\n<>]' context
local -i index
index=$LBUFFER[(I)$~delim]
if (( index <= $#LBUFFER )); then
  context=$LBUFFER[1,index]
  LBUFFER="${context:+$context }${(j:*:)${(Z+C+@)LBUFFER[index+1,-1]}}"
fi
index=$RBUFFER[(i)$~delim]
if (( index <= $#RBUFFER )); then
  context=$RBUFFER[index,-1]
  RBUFFER="${(j:*:)${(Z+C+@)RBUFFER[1,index-1]}}${context:+ $context}"
fi
zle -R

local keymap_menuselect="$(bindkey -M menuselect -L)"
{
  local -A keymap_historysearch=(
    $key[Left] .backward-char
    $key[Right] .forward-char
    $key[ForwardWord] .backward-word
    $key[BackwardWord] .forward-word
    $key[Home] .beginning-of-line
    $key[End] .end-of-line
  )
  local k v; for k v in ${(kv@)keymap_historysearch}; do
    bindkey -M menuselect $k $v
  done

  zle _history_search; local -i ret=$?
  if (( ret > 0 )); then
    LBUFFER=$lbuffer
    RBUFFER=$rbuffer
  fi

} always {
  eval "$keymap_menuselect"
}
return ret
