#!/bin/zsh
local 0=${(%):-%N}

$0() {
  local 0=${(%):-%N}
  local +h curcontext=${curcontext:-${WIDGET}:::}

  local +h -a compprefuncs=( "$compprefuncs[@]" $0.pre )
  local +h -a comppostfuncs=( "$comppostfuncs[@]" $0.post )
  _main_complete
}

$0.pre() {
  compstate[insert]=
  compstate[list]='list force packed rows'
  compstate[old_list]=
}

$0.post() {
  local -i pslines=${#${(%%)PS1}//[^$'\n']}
  local -i realestate=$(( LINES - BUFFERLINES - pslines ))
  _autocomplete__screen_space=$((
    max( _autocomplete__screen_space, min( realestate, compstate[list_lines] ) )
  ))
  if (( compstate[list_lines] > realestate )); then
    compstate[insert]=menu
    MENUSELECT=0
  else
    compstate[insert]=
    unset MENUSELECT
  fi
  compstate[list]='list force packed rows'
}

$0 "$@"
