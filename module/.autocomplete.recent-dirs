#!/bin/zsh
emulate -LR zsh -o noshortloops -o warncreateglobal -o extendedglob
add-zsh-hook precmd .autocomplete.recent-dirs.precmd

.autocomplete.recent-dirs.precmd() {
  emulate -LR zsh -o noshortloops -o warncreateglobal -o extendedglob
  add-zsh-hook -d precmd .autocomplete.recent-dirs.precmd

  if [[ -v functions[zshz] && -v functions[_zshz_precmd] ]] &&
      zstyle -T ':autocomplete:' recent-dirs 'zsh-z'; then

    _autocomplete.recent_dirs() {
      emulate -LR zsh -o noshortloops -o warncreateglobal -o extendedglob
      reply=( "$( zshz --complete -l $1 2> /dev/null )" )
    }

  elif [[ -v commands[zoxide] && -v functions[_zoxide_hook] ]] &&
      zstyle -T ':autocomplete:' recent-dirs 'zoxide'; then

    _autocomplete.recent_dirs() {
      emulate -LR zsh -o noshortloops -o warncreateglobal -o extendedglob
      reply=( "$( zoxide query --list $1 2> /dev/null )" )
    }

  elif [[ -v functions[_zlua] && -v functions[_zlua_precmd] ]] &&
      zstyle -T ':autocomplete:' recent-dirs 'z.lua'; then

    _autocomplete.recent_dirs() {
      emulate -LR zsh -o noshortloops -o warncreateglobal -o extendedglob
      reply=( "${(@)${(f)$( _zlua --complete $1 2> /dev/null )}##[[:digit:].]##[[:space:]]##}" )
    }

  elif [[ -v functions[_z] && -v functions[_z_precmd] ]] &&
      zstyle -T ':autocomplete:' recent-dirs 'z.sh'; then

    _autocomplete.recent_dirs() {
      emulate -LR zsh -o noshortloops -o warncreateglobal -o extendedglob
      reply=( "${(@)${(f)$( _z -l $1 2>&1 )}##(common:|[[:digit:]]##)[[:space:]]##}" )
    }

  elif [[ -v commands[autojump] && -v AUTOJUMP_SOURCED ]] &&
      zstyle -T ':autocomplete:' recent-dirs 'autojump'; then

    _autocomplete.recent_dirs() {
      emulate -LR zsh -o noshortloops -o warncreateglobal -o extendedglob
      reply=( "${(@)${(f)$( autojump --complete $1 2> /dev/null )}##${1}__[[:digit:]]__}" )
    }

  elif [[ ( -v commands[fasd] || -v functions[fasd] ) && -v functions[_fasd_preexec] ]] &&
      zstyle -T ':autocomplete:' recent-dirs 'fasd'; then

    _autocomplete.recent_dirs() {
      emulate -LR zsh -o noshortloops -o warncreateglobal -o extendedglob
      reply=( "$( fasd -dlR $1 2> /dev/null )" )
    }

  else
    autoload -Uz cdr chpwd_recent_dirs
    add-zsh-hook chpwd chpwd_recent_dirs
    zstyle ':chpwd:*' recent-dirs-max 0

    _autocomplete.recent_dirs() {
      emulate -LR zsh -o noshortloops -o warncreateglobal -o extendedglob
      cdr -r
    }
  fi

  if [[ ( -v commands[fasd] || -v functions[fasd] ) && -v functions[_fasd_preexec] ]] &&
      zstyle -T ':autocomplete:' recent-files 'fasd'; then

    _autocomplete.recent_files() {
      emulate -LR zsh -o noshortloops -o warncreateglobal -o extendedglob
      reply=( "$( fasd -flR $1 2> /dev/null )" )
    }
  fi

  autoload +X -Uz _path_files
  functions[_autocomplete.path_files]=$functions[_path_files]

  _path_files() {
    setopt localoptions noshortloops extendedglob

    local ret
    _autocomplete.path_files "$@"
    ret=$?

    if (( compstate[nmatches] > 0 )) || [[ $_completer == complete-fuzzy ]]; then
      _autocomplete.recent_paths
    fi

    return ret
  }
}
