#!/bin/zsh

.autocomplete.base.precmd() {
  emulate -L zsh -o extendedglob

  # Workaround for https://github.com/zdharma/zinit/issues/366
  [[ -v functions[.zinit-shade-off] ]] &&
    .zinit-shade-off "${___mode:-load}"

  add-zsh-hook -d precmd .autocomplete.base.precmd

  .autocomplete.compinit
  add-zsh-hook precmd .autocomplete.screen-space
}

emulate -L zsh -o extendedglob

typeset -gHi _autocomplete__screen_space=$(( LINES - BUFFERLINES - ${#${(f@)PS1}} ))

# Don't let `|` remove suffixes.
[[ -v ZLE_REMOVE_SUFFIX_CHARS ]] ||
  export ZLE_REMOVE_SUFFIX_CHARS=$' \t\n;&'

# Workaround for issue #43
# https://github.com/marlonrichert/zsh-autocomplete/issues/43
zle -N zle-line-finish azhw:zle-line-finish

add-zsh-hook precmd .autocomplete.base.precmd
