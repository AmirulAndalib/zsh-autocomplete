#!/bin/zsh
zmodload zsh/complist
zmodload -Fa zsh/parameter p:funcstack p:functions

.autocomplete.key-binding.precmd() {
  [[ -z $key[Tab] || -z $key[Backtab] ]] &&
    .autocomplete.key

  local tab_style; zstyle -s :autocomplete:tab: widget-style tab_style ||
    tab_style=complete-word

  if zstyle -t :autocomplete:tab: fzf || zstyle -t :autocomplete:tab: fzf-completion; then
    export -H fzf_default_completion=$tab_style

    functions[.autocomplete.fzf-completion]=$functions[fzf-completion]
    fzf-completion() {
      zle() {
        builtin zle "$@" ${${(M)funcstack[2]:#.autocomplete.fzf-completion}:+-w}
      }
      {
        .autocomplete.fzf-completion "$@"
      } always {
        [[ -v functions[zle] ]] &&
          unfunction zle
      }
    }

  else
    bindkey $key[Tab] $tab_style
  fi
  bindkey $key[Backtab] ${tab_style:/menu-complete/reverse-menu-complete}
}

bindkey $key[Control-Space] list-expand
bindkey $key[Up] up-line-or-search
bindkey $key[Down] down-line-or-select
bindkey $key[Alt]$key[Up] up-line
bindkey $key[Alt]$key[Down] down-line
bindkey $key[PageUp] history-search
bindkey $key[PageDown] menu-select

bindkey -M menuselect $key[Tab] menu-complete
bindkey -M menuselect $key[Backtab] reverse-menu-complete

bindkey -M menuselect $key[Return] .accept-line
bindkey -M menuselect $key[Alt]$key[Return] accept-line
bindkey -M menuselect $key[Control-Space] accept-and-hold

case $( bindkey -lL main ) in
  *emacs* )
    bindkey -M menuselect $'\C-[f' .forward-word
    bindkey -M menuselect $'\C-[b' .backward-word
    bindkey -M menuselect $'\C-A' .beginning-of-line
    bindkey -M menuselect $'\C-E' .end-of-line
    ;;
  *vi* )
    bindkey -M menuselect $'\C-[W' .vi-forward-word
    bindkey -M menuselect $'\C-[B' .vi-backward-word
    bindkey -M menuselect $'\C-[0' .vi-digit-or-beginning-of-line
    bindkey -M menuselect $'\C-[$' .vi-end-of-line
    ;;
esac

bindkey -M menuselect $key[Alt]$key[Up] vi-backward-blank-word
bindkey -M menuselect $key[Alt]$key[Down] vi-forward-blank-word
bindkey -M menuselect $key[PageUp] backward-word
bindkey -M menuselect $key[PageDown] forward-word
bindkey -M menuselect $key[Home] beginning-of-history
bindkey -M menuselect $key[End] end-of-history
