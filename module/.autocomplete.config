#!/bin/zsh
zmodload -F zsh/zutil b:zstyle

.autocomplete.config.precmd() {
  # Remove incompatible settings.
  zstyle -d ':completion:*:functions' ignored-patterns
  zstyle -d ':completion:*:*:*:*:*' menu
  zstyle -d '*' single-ignored
  zstyle -d ':completion:*' special-dirs
  zstyle -d ':completion:*:default' list-prompt
  unset LISTPROMPT
}

zstyle ':completion:*' use-cache yes
zstyle ':completion:*' cache-path "$XDG_CACHE_HOME/zsh/compcache"

zstyle ':completion:*' completer _expand _complete \
  _autocomplete.ancestor_dirs _autocomplete.recent_paths \
  _history _correct _ignored
zstyle ':completion:history-incremental-*search-*:*' completer _autocomplete.history_lines

zstyle ':completion:*' old-list shown
zstyle ':completion:*' old-menu no
zstyle ':completion:*' range 10000:1000
zstyle ':completion:*' remove-all-dups yes

# Order of specs in a matcher matters. If we put l:?|=[[:punct:]] before r:|[[:punct:]]=**, the
# matching does not work as expected.
zstyle ':completion:*' matcher-list \
  'r:|[.]=** r:?|[-_]=** l:?|=[-_] m:{[:lower:]-}={[:upper:]_}' \
  'r:|?=** m:{[:lower:]}={[:upper:]}'
zstyle ':completion:list-expand:*' matcher 'l:|=**'
zstyle ':completion:*:options' matcher 'b:-=+'

zstyle ':completion:*' prefix-needed yes
zstyle ':completion:*:users' ignored-patterns '_*'
zstyle ':completion:*:history-(lines|words)' ignore-line current
zstyle ':completion:*' ignore-parents 'parent pwd directory'

zstyle ':completion:*' group-name ''
zstyle ':completion:*' group-order \
  options cd-options \
  executables \
  directories local-directories \
  suffix-aliases \
  aliases functions builtins reserved-words
zstyle ':completion:*' complete-options yes
zstyle ':completion:*' list-dirs-first yes

zstyle ':completion:*:complete:-command-:*' tag-order \
  'directories local-directories executables suffix-aliases' \
  aliases functions 'builtins reserved-words'
zstyle ':completion:list-expand:complete:-command-:*' tag-order '*'
zstyle ':completion:*:(approximate|correct):*' tag-order '! original' -
zstyle ':completion:*:expand:*' tag-order '! all-expansions original' -

zstyle '*:complete:git-*:(|*-)argument-*:*' tag-order \
  'commit(|-^(tag))s tree(|-ish)s (|*-)files (|recent-)branch(|-nam)es'

zstyle -e ':completion:*:expand:*' glob '
  reply=( "no" )
  _autocomplete.is_glob &&
    reply=( "yes" )
'
zstyle ':completion:*' accept-exact continue
zstyle ':completion:*' expand suffix
zstyle ':completion:*' keep-prefix no # Needed for file type highlighting
zstyle ':completion:*' list-suffixes yes
zstyle ':completion:*' path-completion no

local h1=$'%{\e[01;02;39m%}' end=$'%{\e[0m%}' hint=$'%{\e[22;02;39m%}' kbd=$'%{\e[22;39m%}'
zstyle ':completion:*:descriptions' format "$h1%d$end"
zstyle ':completion:*:messages' format "$h1%d$end"
zstyle -e ':completion:*:warnings' format '
  local d=${${(j:, :)_lastdescr[@]:#}/(#m)*, /$MATCH[1,-3] or }
  reply=( "'$h1'No ${tail:+matching }$d completions found.'$end'" )'
zstyle ':completion:*:unambiguous' format \
  "$h1%d$hint  (press ${kbd}Shift${hint}-${kbd}Tab$hint to insert)$end"
zstyle ':completion:*:history-lines' format ''

zstyle ':completion:*' auto-description '%d'

zstyle ':completion:*' insert-sections yes
zstyle ':completion:*' separate-sections yes
