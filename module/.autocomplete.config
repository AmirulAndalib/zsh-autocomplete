#!/bin/zsh

.autocomplete.config.precmd() {
  emulate -L zsh -o extendedglob -o rcquotes
  add-zsh-hook -d precmd .autocomplete.config.precmd

  zmodload zsh/zutil  # `zstyle` builtin

  # Remove incompatible settings.
  zstyle -d ':completion:*:functions' ignored-patterns
  zstyle -d ':completion:*:*:*:*:*' menu
  zstyle -d '*' single-ignored
  zstyle -d ':completion:*' special-dirs

  local -a completers=(
    _expand _complete _complete:-left _complete:-fuzzy  _correct _ignored )
  zstyle ':completion:*' completer _autocomplete.oldlist "$completers[@]" _autocomplete.extras
  zstyle ':completion:list-expand:*' completer "$completers[@]"
  zstyle ':completion:expand-word:*' completer _autocomplete.oldlist _autocomplete.extras

  zstyle ':completion:*:complete:*' matcher-list 'l:|=**'
  zstyle -e ':completion:*:complete:*' ignored-patterns '
    reply=( "^(${(b)_autocomplete__head}(#i)${(b)_autocomplete__tail}*)" )
    if [[ -z "$_autocomplete__tail" ]]; then
      reply=( "${(b)_autocomplete__head}[^[:alnum:]]*" )
    elif [[ $_autocomplete__punct == . ]]; then
      reply=( "^(${(b)_autocomplete__head}*(#i)${(b)_autocomplete__tail}*)" )
    elif [[ -n "$_autocomplete__punct" && -z "$_autocomplete__alnum" ]]; then
      reply=( "^(${(b)_autocomplete__head}${(b)_autocomplete__punct}[[:alnum:]]*)" )
    fi'

  zstyle ':completion:*:complete-left:*' matcher-list '
    l:|=** m:{[:lower:][:upper:]-_}={[:upper:][:lower:]_-}'
  zstyle -e ':completion:*:complete-left:*' ignored-patterns '
    reply=( "${(b)_autocomplete__head}[^[:alnum:]]*" )
    if [[ -n $_autocomplete__punct ]]; then
      reply=( "${(b)_autocomplete__head}([^[:alnum:]]*~${(b)_autocomplete__tail}*)" )
    fi'

  zstyle ':completion:*:(complete-fuzzy|autocomplete.extras):*' matcher-list '
    r:|?=** m:{[:lower:][:upper:]-_}={[:upper:][:lower:]_-}'
  zstyle -e ':completion:*:complete-fuzzy:*' ignored-patterns '
    reply=( "${(b)_autocomplete__head}[^[:alnum:]]*" )
    [[ -n "$_autocomplete__punct" ]] &&
      reply=( "${(b)_autocomplete__head}([^[:alnum:]]*~${(b)_autocomplete__punct[1]}*)" )'

  zstyle -e ':completion:*:-command-:*' tag-order '
    [[ $words[CURRENT] == (|.) ]] &&
      reply=(
        "executables local-directories"
        -
      )
    '
  zstyle ':completion:*:(approximate|correct):*' tag-order '! original' '-'
  zstyle ':completion:*:expand:*' tag-order '! all-expansions original' '-'

  zstyle ':completion:*' file-patterns '
    *(#qD-/):local-directories:"local directory"
    *(#qD^-/):files:file'

  zstyle ':completion::*' group-order '
    directories local-directories path-directories recent-directories
    files recent-files
    history-words'

  zstyle ':completion:*:history-words' ignored-patterns ''
  zstyle ':completion:*:recent-(dirs|files)' ignored-patterns ''
  zstyle ':completion:*:(alias-expansions|history-words|requoted|unambiguous)' ignore-line current

  zstyle -e ':completion:*' glob 'reply=( "true" ) && _autocomplete.is_glob || reply=( "false" )'
  zstyle ':completion:*' expand prefix suffix
  zstyle ':completion:*' keep-prefix false
  zstyle ':completion:*' prefix-needed false
  zstyle ':completion:*' list-suffixes true
  zstyle ':completion:*' path-completion true

  zstyle ':completion:*' menu 'yes select=long-list'

  zstyle ':completion:history-search:*:history-lines' format ''

  local format=$'%{\e[01;02;39m%}'
  local end=$'%{\e[0m%}'
  if zstyle -t :autocomplete: groups always; then
    zstyle ':completion:*' group-name ''
    zstyle ':completion:*:descriptions' format "$format%d:$end"
  fi

  zstyle ':completion:list-expand:*' group-name ''
  zstyle ':completion:list-expand:*:descriptions' format "$format%d:$end"

  local shortcut=$'%{\e[22;39m%}'
  local hint=$'%{\e[02;39m%}'
  local backtab="$shortcut⇤$hint Shift-Tab to insert"
  zstyle ':completion:*:(alias-expansions|requoted|unambiguous)' format \
    "$format%d:  $backtab$end"
  zstyle ':completion:*:history-lines' format \
    "$format%d:  $backtab  $shortcut↑$hint Up Arrow / $shortcut⇞$hint Page Up for more$end"

  zstyle ':completion:*:(alias-expansions|history-words|original|requoted|unambiguous)' \
    group-name ''
  zstyle ':completion:*:recent-directories' group-name ''
  zstyle ':completion:*:recent-files' group-name ''

  zstyle ':completion:*:infos' format '%F{y}%d%f'
  zstyle ':completion:*:messages' format '%F{r}%d%f'
  zstyle ':completion:*:warnings' format '%F{y}%d%f'
  zstyle ':completion:*:errors' format '%F{r}%d%f'

  zstyle ':completion:*' auto-description '%d'

  zstyle ':completion:*' add-space true
  zstyle ':completion:*' list-packed true
  zstyle ':completion:*' list-separator ''
  zstyle ':completion:*' use-cache true

  zstyle ':completion:list-expand:complete:*' matcher-list '
    l:|=** m:{[:lower:][:upper:]-_}={[:upper:][:lower:]_-}' '+r:|?=**'
  zstyle ':completion:list-expand:complete:*' ignored-patterns ''
  zstyle ':completion:list-expand:complete:*:recent-directories' ignored-patterns '/'
  zstyle ':completion:list-expand:*' extra-verbose true
  zstyle ':completion:list-expand:*' menu 'yes select'
}

add-zsh-hook precmd .autocomplete.config.precmd
